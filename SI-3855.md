The following code creates a VerifyError, in particular an Inconsistent stack height runtime error:

```scala
object Broken {
  def byval[A](a: => A) = a
  def main(args: Array[String]) = {
    var n = try { args(0).toInt } catch { case _ => 1 }
    byval(n)
  }
}
```

This may be related to bug #2333 but demonstrates that the problem is not specific to lazy vals.  I cannot seem to figure out what the offending bytecode is, even after writing out what the stack should look like at each point.  Couldn't be a JVM bug, could it?!

Note that
```scala
def byref[A](a: A) = a
```
does not cause the same problem, but
```scala
def byfunc[A](a: () => A) = a()
```
does.  Also, note that vals work fine, as does assigning to a temporary variable (even another var--though this is because the code is then excised into liftedTree$$1, I think).

This code breaks in both 2.8.0.final and r23032.
I think I see what's going on.  Look at the distance between the allocation of the IntRef and the call to its constructor.
```scala
   0:    new    SI-31; //class scala/runtime/IntRef
   3:    dup
   4:    iconst_0
   5:    istore_3
   6:    getstatic    SI-36; //Field scala/Predef$$.MODULE$$:Lscala/Predef$$;
   9:    aload_1
   10:    iconst_0
   11:    aaload
   12:    invokevirtual    SI-40; //Method scala/Predef$$.augmentString:(Ljava/lang/String;)Lscala/collection/immutable/StringOps;
   15:    invokeinterface    SI-46,  1; //InterfaceMethod scala/collection/immutable/StringLike.toInt:()I
   20:    istore_3
   21:    goto    27
   24:    pop
   25:    iconst_1
   26:    istore_3
   27:    iload_3
   28:    invokespecial    SI-49; //Method scala/runtime/IntRef."<init>":(I)V
```
The vm spec says:

"A valid instruction sequence must not have an uninitialized object [...] in a local variable in code protected by an exception handler or a finally clause."

```scala
  Exception table:
   from   to  target type
     6    24    24   any
```
I think the invokespecial instruction at 28 is supposed to be executed before the try block begins, and this process is derailed by the by-name parameter also trying to get involved in lifting n.
I realize iulian will probably wave a wand and solve this in a few minutes, but as an excuse to dig deeper into the bytecode I looked at this for a while.

Using this slightly simplified code:
```scala
class S {
  def g(f: () => Any) = ()
  
  def f = {
    var n = try 17 catch { case _ => 34 }
    g(() => n)
  }
}
```
Which leads to the following for f():
```scala
public void f();
  Code:
   Stack=5, Locals=3, Args_size=1
   0:	new	SI-16; //class scala/runtime/IntRef
   3:	dup
   4:	iconst_0
   5:	istore_2
   6:	ldc	SI-17; //int 17
   8:	istore_2
   9:	goto	16
   12:	pop
   13:	ldc	SI-18; //int 34
   15:	istore_2
   16:	iload_2
   17:	invokespecial	SI-22; //Method scala/runtime/IntRef."<init>":(I)V
   20:	astore_1
   21:	aload_0
   22:	new	SI-24; //class bug3855/S$$$$anonfun$$f$$1
   25:	dup
   26:	aload_0
   27:	aload_1
   28:	invokespecial	SI-27; //Method bug3855/S$$$$anonfun$$f$$1."<init>":(Lbug3855/S;Lscala/runtime/IntRef;)V
   31:	invokevirtual	SI-31; //Method g:(Lscala/Function0;)V
   34:	return
```
Locals:
```scala
LocalVariableTable:
  Start Length Slot Name               Descriptor            Generic-signature
  0     35     0    this               Lbug3855/S;           
  6     11     2    exceptionResult1   I                     
  21    13     1    n$$1                Lscala/runtime/IntRef;
```
The space for the IntRef is allocated and then code is executed to obtain the constructor argument.  That code extends all the way to inst 16, which is also the point at which the verifier gives a failing grade.

Via the non-exceptional path, instr 9 jumps to 16 and the stack height is 2: the dup-ed IntRef.  If the exception is thrown, execution is resumed at 12 with a stack consisting only of the exception, which is popped off.  When it reaches instr 16 there is nothing there.  2 != 0 so fail.

I conclude it should look more like:
```scala
// first the try/catch expression
4:	iconst_0
5:	istore_2
6:	ldc	SI-17; //int 17
8:	istore_2
9:	goto	16
12:	pop
13:	ldc	SI-18; //int 34
15:	istore_2

// then the intref creation
0:	new	SI-16; //class scala/runtime/IntRef
3:	dup
16:	iload_2
17:	invokespecial	SI-22; //Method scala/runtime/IntRef."<init>":(I)V
```
A-ha.  Uncurry:
```scala
// a local variable that is mutable and free somewhere later should be lifted
// as lambda lifting (coming later) will wrap 'rhs' in an Ref object.
if (!sym.owner.isSourceMethod || (sym.isVariable && freeMutableVars(sym)))
  withNeedLift(true) { super.transform(tree) }
else
  super.transform(tree)  
```
So that is where var n in my example is supposed to be lifted, but it is not because it does not appear in freeMutableVars.  The logic in freeLocalsTraverser is incomplete.  I'm attaching a hacked together attempt to improve it: it needs more work but should be in the ballpark.  With the patch in place the submitter's test case runs without verify error as does my example in comments.
